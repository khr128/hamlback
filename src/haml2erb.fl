/*%option   8bit reentrant bison-bridge*/
%option   warn nodefault
%option   yylineno
%option   outfile="haml2erb.scanner.c" header-file="haml2erb.scanner.h"
%{
#include <stdio.h>
#include "haml2erb.tab.h"
%}

NUM                 [0-9]
VAR                 [a-zA-Z]
VARNUM              [_0-9a-zA-Z]
CONTENT             [ _0-9a-zA-Z.,<>(){}\[\]\-="'!?/:#]
SPACE               [ ]
TAB                 \t
WS                  [ \t]
NOT_WS              [^ \t]
PCT                 %
POUND               #
OPEN_BRACE          \{
CLOSE_BRACE         \}
COLON               \:
ARROW               =>
EQUAL               =
DASH                -
COMMA               , 
PERIOD              \. 
DASH_POUND          -#
SLASH               \/
ESCAPE              \\
QUOTE               [\'\"]
NOT_QUOTE           [^\'\"]
LINE_CONTINUATION   \|\n
DOCTYPE             !!!

%%
^{DOCTYPE}                                        { return DOCTYPE;}
^{SPACE}*{ESCAPE}.*                               { yylval.strval = strdup(yytext); return ESCAPED_CONTENT;}
^{SPACE}*{SLASH}.*                                { yylval.strval = strdup(yytext); return HTML_COMMENT;}
{DASH_POUND}.*\n                                  { return HAML_COMMENT;}
^{SPACE}*{EQUAL}.*                                { yylval.strval = strdup(yytext); return RUBY_CODE;}
^{SPACE}*{DASH}.*{LINE_CONTINUATION}?             { yylval.strval = strdup(yytext); return RUBY_CODE_NO_INSERT;}
{LINE_CONTINUATION}                               { return LINE_CONTINUATION; }
^{SPACE}+{EQUAL}.*/{LINE_CONTINUATION}            { yylval.strval = strdup(yytext); return RUBY_CODE;}
.*/{LINE_CONTINUATION}                            { yylval.strval = strdup(yytext); return CONTENT;}
^{SPACE}+                                         { yylval.strval = strdup(yytext); return SPACE_INDENT; }
{EQUAL}                                           { return EQUAL; }
{COMMA}{SPACE}*                                   { return COMMA; }
{PCT}                                             { return PCT; }
{POUND}                                           { return POUND; }
{PERIOD}                                          { return PERIOD; }
{OPEN_BRACE}                                      { return OPEN_BRACE; }
{COLON}{VARNUM}+                                  { yylval.strval = strdup(yytext); return SYMBOL; }
{COLON}{QUOTE}{NOT_QUOTE}*{QUOTE}                 { yylval.strval = strdup(yytext); return QUOTED_SYMBOL; }
{ARROW}                                           { return ARROW; }
{CLOSE_BRACE}                                     { return CLOSE_BRACE; }
^{SPACE}*\n                                       { return BLANK_LINE; }
\n                                                { return EOL; }
{VAR}+{VARNUM}*                                   { yylval.strval = strdup(yytext); return VAR; }
{WS}+{CONTENT}+                                   { yylval.strval = strdup(yytext); return CONTENT; }
{QUOTE}{NOT_QUOTE}*{QUOTE}                        { yylval.strval = strdup(yytext); return STRING; }
.                                                 { yylval.strval = strdup(yytext); return CONTENT;} 
<<EOF>>                                           { fprintf (stderr, "End of parse.\n" ); yyterminate(); }
%%

yyerror( char *str )
{
  fprintf(stderr, "error:-( %s at line %d about '%s'\n", str, yylineno, yytext); 
}


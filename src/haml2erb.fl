/*%option   8bit reentrant bison-bridge*/
%option   warn nodefault
%option   yylineno
%option   outfile="haml2erb.scanner.c" header-file="haml2erb.scanner.h"
%{
#include <stdio.h>
#include "haml2erb.tab.h"
%}

NUM           [0-9]
VAR           [a-zA-Z]
VARNUM        [_0-9a-zA-Z]
WS            [ ]
TAB           \t
PCT           %
POUND         #
OPEN_BRACE    \{
CLOSE_BRACE   \}
COLON         \:
ARROW         =>
QUOTE         [\'\"]
NOT_QUOTE     [^\'\"]

%%
^{WS}+                        { yylval.strval = strdup(yytext); return SPACE_INDENT; }
{PCT}                         { return PCT; }
{POUND}                       { return POUND; }
{OPEN_BRACE}                  { return OPEN_BRACE; }
{COLON}{VARNUM}+              { yylval.strval = strdup(yytext); return SYMBOL; }
{ARROW}                       { return ARROW; }
{QUOTE}{NOT_QUOTE}*{QUOTE}    { yylval.strval = strdup(yytext); return STRING; }
{CLOSE_BRACE}                 { return CLOSE_BRACE; }
\n                            { return EOL; }
{VAR}+{VARNUM}*               { yylval.strval = strdup(yytext); return VAR; }
{PCT}+{NUM}+{VAR}+{VARNUM}*   { fprintf (stderr, "Invalid tag name match: %s\n", yytext); return INVALID; }
{NUM}+{VAR}+{VARNUM}*         { fprintf (stderr, "Invalid VAR match: %s\n", yytext); return INVALID; }
{NUM}+                        { fprintf (stderr, "NUM match: %s\n", yytext); return INVALID; }
.                             { fprintf (stderr, "default match: %s\n", yytext); return INVALID;} 
<<EOF>>                       { fprintf (stderr, "End of parse.\n" ); yyterminate(); }
%%

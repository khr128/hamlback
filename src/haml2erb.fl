/*%option   8bit reentrant bison-bridge*/
%option   warn nodefault
%option   yylineno
%option   outfile="haml2erb.scanner.c" header-file="haml2erb.scanner.h"
%{
#include <stdio.h>
#include "haml2erb.tab.h"
%}

NUM           [0-9]
VAR           [a-zA-Z]
VARNUM        [_0-9a-zA-Z]
CONTENT       [ _0-9a-zA-Z.]
SPACE         [ ]
TAB           \t
WS            [ \t]
NOT_WS        [^ \t]
PCT           %
POUND         #
OPEN_BRACE    \{
CLOSE_BRACE   \}
COLON         \:
ARROW         =>
EQUAL         =
QUOTE         [\'\"]
NOT_QUOTE     [^\'\"]

%%
^{SPACE}+                     { yylval.strval = strdup(yytext); return SPACE_INDENT; }
{EQUAL}                       { return EQUAL; }
{PCT}                         { return PCT; }
{POUND}                       { return POUND; }
{OPEN_BRACE}                  { return OPEN_BRACE; }
{COLON}{VARNUM}+              { yylval.strval = strdup(yytext); return SYMBOL; }
{ARROW}                       { return ARROW; }
{QUOTE}{NOT_QUOTE}*{QUOTE}    { yylval.strval = strdup(yytext); return STRING; }
{CLOSE_BRACE}                 { return CLOSE_BRACE; }
\n                            { return EOL; }
{VAR}+{VARNUM}*               { yylval.strval = strdup(yytext); return VAR; }
{WS}+{CONTENT}+               { yylval.strval = strdup(yytext); return CONTENT; }
{PCT}+{NUM}+{VAR}+{VARNUM}*   { fprintf (stderr, "Invalid tag name match: %s\n", yytext); return INVALID; }
.                             { fprintf (stderr, "default match: %s\n", yytext); return INVALID;} 
<<EOF>>                       { fprintf (stderr, "End of parse.\n" ); yyterminate(); }
%%

yyerror( char *str )
{
  fprintf(stderr, "error:-( %s at line %d about '%s'\n", str, yylineno, yytext); 
}

